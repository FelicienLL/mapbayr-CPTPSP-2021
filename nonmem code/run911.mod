$PROBLEM IBRUTINIB GALLAIS
$INPUT ID TIME EVID AMT CMT II SS RATE MDV DV CYCLE S2_SAMPLING
$DATA data_to_fit911.csv IGNORE=@
$SUBROUTINES ADVAN5 SS5

$MODEL
COMP=(DEPOT,DEFDOSE)
COMP=(IBRU_CENT,DEFOBS)
COMP=(IBRU_PERIPH)
COMP=(DHDIBRU)
COMP=(DHDIBRU_PERIPH)

$PK 
iETA1 = 0
iETA2 = 0
iETA3 = 0
iETA4 = 0
iETA5 = 0
iETA6 = 0
iETA7 = 0
iETA8 = 0
iETA9 = 0
iETA10 = 0
iETA11 = 0
iETA12 = 0
OCC1=0
OCC2=0
IF(CYCLE.EQ.1) OCC1=1
IF(CYCLE.EQ.2) OCC2=1

;IBRU
CLMET  = THETA(1)  * EXP(ETA(6) + iETA6)
V2     = THETA(2)  * EXP(ETA(2) + iETA2)
KA     = THETA(3)
Q      = THETA(4)
V3     = THETA(5)  * EXP(ETA(1) + iETA1)
ALAG1  = THETA(6)  * EXP(ETA(7) + iETA7)
D1     = THETA(7)  * EXP(ETA(8) + iETA8)
CLIBRU = THETA(8)  * EXP(ETA(4) + iETA4 + (ETA(9)  + iETA9 ) * OCC1 + (ETA(10) + iETA10) * OCC2)

;DHD-IBRU
CLDHD  = THETA(9)  * EXP(ETA(3) + iETA3 + (ETA(11) + iETA11) * OCC1 + (ETA(12) + iETA12) * OCC2)
V4     = V2
V5     = V3
K14    = THETA(10) * EXP(ETA(5) + iETA5)
QDHD   = THETA(11)

K12 = KA
K23 = Q / V2
K32 = Q / V3
K24 = CLMET / V2
K40 = CLDHD / V4
K20 = CLIBRU / V2
K45 = QDHD / V4
K54 = QDHD / V5

S2 = V2 / 1000
S4 = V4 / 1000

$THETA
149.622 FIXED ; 1 CL MET
1005.03 FIXED ; 2 V2
1.55664 FIXED ; 3 KA
171.094 FIXED ; 4 Q
1481.01 FIXED ; 5 V3
.238192 FIXED ; 6 ALAG1
.988671 FIXED ; 7 D1
241.899 FIXED ; 8 CLIBRU
180.972 FIXED ; 9 CLDHD
1.20812 FIXED ;10 K14
50.3012 FIXED ;11 QDHD

$OMEGA BLOCK(6)
0.465173 FIXED ; iiv V3
0.432131 0.512330000 ; iiv V2
0.220876 0.217668000 0.228809 ; iiv CLDHD
0.374779 0.421162000 0.195424 0.3658180  ; iiv CLIBRU
0.298891 0.287158000 0.156798 0.2487940 0.345464  ; iiv K14
0.000000 0.000591386 0.180014 0.0217671 0.129532 0.347017000 ; iiv CLMET


$OMEGA BLOCK(2)
0.501473 FIXED ; iiv ALAG1
0.282336 0.844851 ; iiv D1
$OMEGA BLOCK(1) 0.197216 FIXED;IOV CLIBRU
$OMEGA BLOCK(1) SAME ;IOV CLIBRU
$OMEGA BLOCK(1) 0.0637684 FIXED;IOV CLDHD
$OMEGA BLOCK(1) SAME ;IOV CLDHD

$SIGMA
0.128496  FIXED ; err IBRU
0.0639887 FIXED ; err DHD

$ERROR
DEL=0
IF (F.EQ.0) DEL=1
W=F+DEL
Y = F
IF(CMT.EQ.2) Y=F+W*EPS(1)
IF(CMT.EQ.4) Y=F+W*EPS(2)
IPRED=F
IRES=DV-IPRED
IWRES=IRES/W

$EST METHOD=1 INTER NOABORT MAXEVAL=0 FORMAT= s1PE16.8E3
$COV UNCONDITIONAL


$TABLE ID TIME EVID AMT CMT II SS RATE MDV DV CYCLE S2_SAMPLING PRED IPRED NOPRINT NOAPPEND ONEHEADER FORMAT=s1PE16.8E3 FILE=run911.tab
