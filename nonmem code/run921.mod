$PROBLEM PAZOPANIB YU
$INPUT ID TIME EVID AMT CMT II ADDL MDV DV CYCLE AOLA TOLA S2_SAMPLING
$DATA data_to_fit921.csv IGNORE=@
$SUBROUTINES ADVAN13 TOL=8

$MODEL 
COMP=(DEPOT1)
COMP=(CENTRAL)
COMP=(DEPOT2)
COMP=(PERIPH)

$PK 
iETA1 = 0
iETA2 = 0
iETA3 = 0
iETA4 = 0
iETA5 = 0
iETA6 = 0
TVCL     = THETA(1)
TVV2     = THETA(2)
TVKAF    = THETA(3)
TVKAS    = THETA(4)
TVALAG3  = THETA(5)
TVQ24    = THETA(6)
TVV4     = THETA(7)
TVLAMBDA = THETA(8) 
TVDCRP   = THETA(9)
TVED50   = THETA(10)
TVEMAX   = THETA(11)
TVFR     = THETA(12)
ERRADD   = THETA(13)
ERRPROP  = THETA(14)

ETCL  = ETA(1) + iETA1
ETKAF = ETA(2) + iETA2
ETV4  = ETA(3) + iETA3
ETF   = ETA(4) + iETA4

IOV1  = ETA(5) + iETA5
IOV2  = ETA(6) + iETA6

IOVTVF = 0
;IF(CYCLE.EQ.200)   IOVTVF = IOV2
IF(CYCLE.EQ.1)     IOVTVF = IOV1
;IF(CYCLE.EQ.1.5)   IOVTVF = IOV2
IF(CYCLE.EQ.2)     IOVTVF = IOV2

CL     = TVCL  *EXP(ETCL)
V2     = TVV2
KAF    = TVKAF *EXP(ETKAF)
KAS    = TVKAS
ALAG3  = TVALAG3
ALAG1  = 0.000001
Q24    = TVQ24
V4     = TVV4  *EXP(ETV4)

LAMBDA = TVLAMBDA / 24
DCRP   = TVDCRP

ED50   = TVED50
EMAX   = TVEMAX

TVFT   = 1-DCRP+DCRP*EXP(-LAMBDA*TOLA)                      ; TVFT = Val typique biodisponibilite relative temps-dependante 
TVFD   = (1- (AOLA - 200)*EMAX / (ED50 + AOLA - 200)) ; TVFD = Val typique biodisponibilite relative dose-dependante 
TVFI   = TVFD*TVFT*EXP(ETF)                                 ; TVFD x TVFT + var inter-individuelle
TVF    = TVFI*EXP(IOVTVF)                                   ; + var inter-occasion

;Fraction repartie dans l'un et l'autre des compartiments
FR = TVFR
F1 = TVF*(FR)
F3 = TVF*(1-FR)

K20 = CL/V2
K24 = Q24/V2
K42 = Q24/V4
S2  = V2

$DES 
DADT(1) = - KAF*A(1)
DADT(2) =   KAF*A(1) + KAS*A(3) - K20*A(2) - K24*A(2) + K42*A(4)
DADT(3) = - KAS*A(3)
DADT(4) =   K24*A(2) - K42*A(4)

$ERROR
IPRED=F
W=SQRT(ERRADD**2+(ERRPROP*IPRED)**2)
Y=IPRED+W*EPS(1)
IRES=DV-IPRED
IWRES=IRES/(W+0.001)


$THETA
(0, 0.27) FIX ; 1  CL
(0, 2.43) FIX ; 2  V2
(0, 0.40) FIX ; 3  KAF
(0, 0.12) FIX ; 4  KAS
(0, 0.98) FIX ; 5  ALAG3
(0, 0.99) FIX ; 6  Q24
(0, 25.1) FIX ; 7  V4
(0, 0.15) FIX ; 8  LAMBDA
(0, 0.501) FIX ; 9  DCRP
(480) FIX ; 10 ED50
(1) FIX ; 11 EMAX
(0.361) FIX ; 12 FR
(3.100) FIX ; 13 ADD
(0, 0.08) FIX ; 14 PROP

$OMEGA 0.095481 FIX  ; 1 CL
$OMEGA 1.96 FIX  ; 2 KAF
$OMEGA 0.964324 FIX  ; 3 V4
$OMEGA 0.126736 FIX  ; 4 F
$OMEGA BLOCK(1) 0.555025 FIX  ; 5 IOVTVF
$OMEGA BLOCK(1) SAME   ; 6 IOVTVF

$SIGMA 1 FIX 

$EST METHOD=1 INTER NOABORT MAXEVAL=0 FORMAT= s1PE16.8E3
$COV UNCONDITIONAL


$TABLE ID TIME EVID AMT CMT II ADDL MDV DV CYCLE AOLA TOLA S2_SAMPLING PRED IPRED NOPRINT NOAPPEND ONEHEADER FORMAT=s1PE16.8E3 FILE=run921.tab